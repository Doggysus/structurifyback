plugins {
	id 'java'
	id 'idea'
	id 'dev.architectury.loom'
	id 'architectury-plugin'
}

def minecraftVersion = stonecutter.current.version

def modrinth(String name, String version) {
	return "maven.modrinth:$name:$version"
}

def prop(String name) {
	return rootProject.project(stonecutter.current.project).property(name).toString()
}

architectury {
	common prop('enabled_platforms').split(',')
}

loom {
	silentMojangMappingsLicense()

	accessWidenerPath = file("../../src/main/resources/structurify.accesswidener")

	runConfigs.all {
		ideConfigGenerated true
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	mappings "net.fabricmc:yarn:${prop('yarn_mappings')}:v2"

	// General
	modImplementation("net.fabricmc:fabric-loader:${prop('fabric_loader_version')}")
	implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${prop('mixin_extras_version')}"))

	// Required client
	if (stonecutter.compare(minecraftVersion, '1.20.6') < 0) {
		modImplementation(modrinth("yacl", "${prop('yacl_version')}-fabric"))
	} else {
		modImplementation(modrinth("yacl", "${prop('yacl_version')}-fabric"))
	}

	// Optional
	if (minecraftVersion == '1.20.1') {
		modCompileOnly("curse.maven:drp-global-datapack-317134:4570601")
	} else if (minecraftVersion == '1.20.2') {
		modCompileOnly("curse.maven:drp-global-datapack-317134:5374072")
	} else if (minecraftVersion == '1.20.4') {
		modCompileOnly("curse.maven:drp-global-datapack-317134:5374072")
	}
}

java {
	withSourcesJar()

	def javaVersion = (stonecutter.compare(stonecutter.current.version, "1.20.6") >= 0) ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
	sourceCompatibility = javaVersion
	targetCompatibility = javaVersion
}

tasks.withType(JavaCompile).configureEach {
	def releaseVersion = (stonecutter.compare(stonecutter.current.version, "1.20.6") >= 0) ? 21 : 17;
	it.options.release = releaseVersion
}

repositories {
	// CurseForge
	maven {
		name 'CurseForge'
		url "https://www.cursemaven.com"
	}

	// Modrinth
	maven {
		url "https://api.modrinth.com/maven"
		content {
			includeGroup("maven.modrinth")
		}
	}

	// Mixin Extras
	maven {
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.llamalad7'
		}
	}

	// YACL
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://maven.isxander.dev/snapshots" }
	maven { url 'https://thedarkcolour.github.io/KotlinForForge/' }
}

if (stonecutter.current.isActive) {
	rootProject.tasks.register("buildActiveCommon") {
		group = "project"
		dependsOn(tasks.named("build"))
	}
}